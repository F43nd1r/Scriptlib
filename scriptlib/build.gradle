plugins {
    id 'com.android.library'
    id 'digital.wup.android-maven-publish' version '3.6.2'
    id 'com.jfrog.bintray' version '1.8.3'
    id 'net.researchgate.release' version '2.7.0'
}

android {
    compileSdkVersion Integer.parseInt(androidTargetVersion)

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion androidTargetVersion
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
dependencies {
    implementation 'androidx.annotation:annotation:1.1.0-beta01'
    implementation("com.google.guava:guava:27.1-android")
    implementation 'com.josesamuel:remoter-annotations:1.2.0'
    annotationProcessor 'com.josesamuel:remoter:1.2.0'
    implementation 'org.parceler:parceler-api:1.1.12'
    annotationProcessor 'org.parceler:parceler:1.1.12'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs + files("${project.buildDir}/generated/source/aidl/release") + files("${project.buildDir}/generated/source/apt/release")
    classpath += files(android.getBootClasspath().join(File.pathSeparator)) + files("${project.buildDir}/generated/source/r/release")
    options.with {
        linksOffline "http://d.android.com/reference", "${android.sdkDirectory.path}/docs/reference"
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.android
            artifact sourcesJar
            artifact javadocJar
            groupId = 'com.faendir.lightninglauncher'
            artifactId = 'scriptlib'
            version = version
            pom {
                name = 'scriptlib'
                description = 'Allows to import scripts directly into Lightning Launcher'
                url = 'https://github.com/F43nd1r/Scriptlib'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'f43nd1r'
                        name = 'Lukas Morawietz'
                        email = 'support@faendir.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/F43nd1r/Scriptlib.git'
                    developerConnection = 'scm:git:git@github.com:F43nd1r/Scriptlib.git'
                    url = 'https://github.com/F43nd1r/Scriptlib'
                }
            }
        }
    }
}

bintray {
    user = artifactoryUser
    key = artifactoryApiKey
    publications = ['maven']
    pkg {
        repo = 'maven'
        afterEvaluate {
            org.gradle.api.publish.maven.MavenPom pom = publishing.publications.maven.pom
            name = pom.name.get()
            websiteUrl = pom.url.get()
            vcsUrl = pom.scm.url.get()
            licenses = pom.licenses.collect { l -> l.name.get() }
            desc = pom.description.get()
        }
        publicDownloadNumbers = true
        version {
            name = project.version
        }
    }
}



release {
    tagTemplate = '$name-$version'
}
afterReleaseBuild.dependsOn bintrayUpload



